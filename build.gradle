plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'java'
}

group = 'org.sodemc'
version = '0.1.3'

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven { url "https://libraries.minecraft.net" }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT"
    implementation "net.kyori:adventure-text-minimessage:4.14.0"
    compileOnly 'me.clip:placeholderapi:2.11.5'
    implementation 'com.github.Revxrsal.Lamp:common:3.1.8'
    implementation("com.github.Revxrsal.Lamp:bukkit:3.1.8")
    implementation 'me.lucko:commodore:2.2'
}

shadowJar {
    dependencies {
        exclude(dependency('com.mojang:brigadier'))
    }

    relocate 'me.lucko.commodore', 'org.sodemc.bytetag.commodore'
}

compileJava {
    options.compilerArgs += ["-parameters"]
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
